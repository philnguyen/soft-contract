#lang racket
(require
 racket/unsafe/ops
 web-server/private/util
 "../utils/main.rkt" "../primitives/declarations.rkt" "../ast/main.rkt"
 ;; For extra constants
 racket/undefined racket/extflonum
 (only-in redex/reduction-semantics variable-not-in)
 syntax/parse syntax/modresolve
 "expand.rkt"
 (prefix-in fake: "../fake-contract.rkt")
 (for-syntax racket/base racket/match racket/list racket/set syntax/parse racket/contract
             "../primitives/declarations.rkt"))
(provide (all-defined-out))

(define/contract (files->modules paths)
  ((listof path-string?) . -> . (listof -module?))
  (map (compose1 parse-top-level-form do-expand-file) paths))

(define/contract cur-mod (parameter/c string? #|TODO|#)
  (make-parameter "top-level"))

(define scv-syntax? (and/c syntax? (not/c scv-ignore?)))

(define (mod-path->mod-name p)
  (match p ; hacks
    ['#%kernel 'Î›]
    ['#%unsafe 'unsafe]
    [(and (? symbol?) (app symbol->string "expanded module")) (cur-mod)]
    [_ (path->string (simplify-path p))]))

;; Convert syntax to `top-level-form`
(define/contract (parse-top-level-form form)
  (scv-syntax? . -> . -top-level-form?)
  (log-debug "parse-top-level-form:~n~a~n~n" (pretty (syntax->datum form)))
  (syntax-parse form
    [((~literal module) id path (#%plain-module-begin forms ...))
     (define mod-name (mod-path->mod-name (syntax-source #'id)))
     (-module
      mod-name
      (parameterize ([cur-mod mod-name])
        (filter
         values
         (for/list ([formáµ¢ (syntax->list #'(forms ...))]
                    #:when
                    (syntax-parse formáµ¢
                      [((~literal module) (~literal configure-runtime) _ ...) #f]
                      [_ #t])
                    #:when
                    (scv-syntax? formáµ¢))
           (parse-module-level-form formáµ¢)))))]
    [((~literal begin) form ...)
     (-begin/simp (map parse-top-level-form (syntax->list #'(form ...))))]
    [((~literal #%expression) e) (parse-e #'e)]
    [_ (parse-general-top-level-form form)]))

;; Convert syntax to `module-level-form`. May fail for unsupported forms.
(define/contract (parse-module-level-form form)
  (scv-syntax? . -> . (or/c #f -module-level-form?))
  (log-debug "parse-module-level-form:~n~a~n~n" (pretty (syntax->datum form)))
  (syntax-parse form
    #:literals (#%provide begin-for-syntax #%declare #%plain-lambda #%plain-app
                call-with-values)
    [(#%provide spec ...)
     (-provide (map parse-provide-spec (syntax->list #'(spec ...))))]
    [(#%declare _ ...) (todo '#%declare)]
    [(begin-for-syntax _ ...) #|ignore|# #f]
    
    ;; Hack for reading our fake-contracts:
    [(#%plain-app
      call-with-values
      (#%plain-lambda
       ()
       (#%plain-app
        (~literal fake:dynamic-provide/contract)
        (#%plain-app (~literal list) x:id c:expr) ...))
      _)
     (-provide (for/list ([x (syntax->datum #'(x ...))]
                          [c (syntax->list #'(c ...))])
                 (-p/c-item x (parse-e c) (+â„“!))))]
    
    [_ (or (parse-general-top-level-form form)
           (parse-submodule-form form))]))

(define/contract (parse-submodule-form form)
  (scv-syntax? . -> . (or/c #f -submodule-form?))
  (log-debug "parse-submodule-form:~n~a~n~n" (pretty (syntax->datum form)))
  (syntax-parse form
    [((~literal module) id path ((~literal #%plain-module-begin) d ...))
     (-module
      (path->string (simplify-path (syntax-source #'id)))
      (map parse-module-level-form (syntax->list #'(d ...))))]
    [((~literal module*) _ ...) (todo 'module*)]
    [_ #f]))

(define/contract (parse-general-top-level-form form)
  (scv-syntax? . -> . (or/c #f -general-top-level-form?))
  (log-debug "parse-general-top-level-form:~n~a~n" (pretty (syntax->datum form)))
  (syntax-parse form
    #:literals (define-syntaxes define-values #%require let-values #%plain-app values
                call-with-values #%plain-lambda)
    ;; HACK against shits generated by recent versions of Racket (6.4+)
    ;; TODO: figure out proper way to handle this.
    [(define-values _ (#%plain-app do-partial-app _ ...))
     #:when (equal? 'do-partial-app (syntax->datum #'do-partial-app))
     #f]
    [(#%plain-app call-with-values (#%plain-lambda () e) print-values:id)
     #:when (equal? 'print-values (syntax->datum #'print-values))
     (parse-e #'e)]

    [(define-values (_ ctor pred acc ...)
       (let-values ([(_ ...)
                     (let-values ()
                       (let-values ()
                         (#%plain-app _ ctor-name _ ...)))])
         (#%plain-app values _ _ _ (#%plain-app _ _ _ _) ...)))
     ;(define/contract ctor-name symbol? (syntax-e #'ctor-name))
     (define/contract ctor-name symbol? (syntax-e #'ctor))
     (define/contract accs (listof identifier?) (syntax->list #'(acc ...)))
     (define n (length accs))
     (define si (-struct-info (-ð’¾ ctor-name (cur-mod)) n âˆ…))
     (-define-values
      (list* ctor-name (syntax-e #'pred) (map syntax-e accs))
      (-@ (-ref (-ð’¾ 'values 'Î›) (+â„“!))
          (list* (-st-mk si)
                 (-st-p si)
                 (for/list ([(accáµ¢ i) (in-indexed accs)])
                   (-st-ac si i)))
          0))]
    [(define-values (x:identifier) e) ; FIXME: separate case hack to "close" recursive contract
     (define lhs (syntax-e #'x))
     (define rhs (parse-e #'e))
     (define frees (free-x/c rhs))
     (cond
       [(set-empty? frees)
        (-define-values (list lhs) rhs)]
       [(set-empty? (set-remove frees lhs))
        (define pos (+â„“!))
        (-define-values (list lhs)
           (-Î¼/c pos (e/ (-x/c.tmp lhs) (-x/c pos) rhs)))]
       [else
        (error 'TODO
               "In ~a's definition: arbitrary reference (recursive-contract ~a) not supported for now."
               lhs (set-first (set-remove frees lhs)))])]
    [(define-values (x:identifier ...) e)
     (-define-values (syntax->datum #'(x ...)) (parse-e #'e))]
    [(#%require spec ...)
     (-require (map parse-require-spec (syntax->list #'(spec ...))))]
    [(define-syntaxes _ ...) #f] 
    [_
     (parse-e form)]))

(define/contract (parse-e stx)
  (scv-syntax? . -> . -e?)
  (log-debug "parse-e: ~a~n~n" (pretty-format (syntax->datum stx)))

  (define/contract (parse-es es)
    ((and/c scv-syntax? (not/c identifier?)) . -> . (listof -e?))
    (map parse-e (syntax->list es)))

  (syntax-parse stx
    #:literals
    (let-values letrec-values begin begin0 if #%plain-lambda #%top
     module* module #%plain-app quote #%require quote-syntax
     with-continuation-mark #%declare #%provide case-lambda
     #%variable-reference set! list)

    ;; HACK for incomplete pattern matching error
    [(#%plain-app f _ ...)
     #:when (equal? 'match:error (syntax->datum #'f))
     (-error "incomplete pattern matching")]

    ;;; Contracts
    ;; Non-dependent function contract
    [(let-values ([(_) (~literal fake:dynamic->*)]
                  [(_) (#%plain-app list c ...)]
                  [(_) (#%plain-app list d)])
       _ ...)
     (--> (parse-es #'(c ...)) (parse-e #'d) (+â„“!))]
    ;; Dependent contract
    [(~or (begin
            (#%plain-app
             (~literal fake:dynamic->i)
             (#%plain-app list [#%plain-app list (quote x:id) câ‚“:expr] ...)
             (#%plain-lambda (z:id ...) d:expr #|FIXME temp hack|# _ ...))
            _ ...)
          (let-values ()
            (#%plain-app
             (~literal fake:dynamic->i)
             (#%plain-app list [#%plain-app list (quote x:id) câ‚“:expr] ...)
             (#%plain-lambda (z:id ...) d:expr #|FIXME temp hack|# _ ...))
            _ ...)
          (#%plain-app
           (~literal fake:dynamic->i)
           (#%plain-app list [#%plain-app list (quote x:id) câ‚“:expr] ...)
           (#%plain-lambda (z:id ...) d:expr #|FIXME temp hack|# _ ...)))
     (define cs (parse-es #'(câ‚“ ...)))
     (define mk-d (-Î» (syntax->datum #'(z ...)) (parse-e #'d)))
     (-->i cs mk-d (+â„“!))]
    ;; independent varargs
    [(let-values ([(_) (~literal fake:dynamic->*)]
                  [(_) (#%plain-app list inits ...)]
                  [(_) rst]
                  [(_) rng])
       _ ...)
     (-->* (map parse-e (syntax->list #'(inits ...)))
           (parse-e #'rst)
           (parse-e #'rng))]
    [(#%plain-app (~literal fake:listof) c)
     (-listof (parse-e #'c))]
    [(#%plain-app (~literal fake:list/c) c ...)
     (-list/c (parse-es #'(c ...)))]
    [(#%plain-app (~literal fake:box/c) c)
     (-box/c (parse-e #'c))]
    [(#%plain-app (~literal fake:vector/c) c ...)
     (-@ (-ref (-ð’¾ 'vector/c 'Î›) (+â„“!))
         (parse-es #'(c ...))
         (+â„“!))]
    [(#%plain-app (~literal fake:vectorof) c)
     (-@ (-ref (-ð’¾ 'vectorof 'Î›) (+â„“!))
         (list (parse-e #'c))
         (+â„“!))]
    [(begin (#%plain-app (~literal fake:dynamic-struct/c) tag:id c ...) _ ...)
     (define si (-struct-info (-ð’¾ (syntax-e #'tag) (cur-mod))
                              (length (syntax->list #'(c ...)))
                              âˆ…))
     (-struct/c si (parse-es #'(c ...)) (+â„“!))]
    [(#%plain-app (~literal fake:=/c) c) (-comp/c '= (parse-e #'c))]
    [(#%plain-app (~literal fake:>/c) c) (-comp/c '> (parse-e #'c))]
    [(#%plain-app (~literal fake:>=/c) c) (-comp/c '>= (parse-e #'c))]
    [(#%plain-app (~literal fake:</c) c) (-comp/c '< (parse-e #'c))]
    [(#%plain-app (~literal fake:<=/c) c) (-comp/c '<= (parse-e #'c))]
    [(#%plain-app (~literal fake:cons/c) c d)
     (-cons/c (parse-e #'c) (parse-e #'d))]
    [(#%plain-app (~literal fake:one-of/c) c ...)
     (-one-of/c (parse-es #'(c ...)))]
    [(~or (let-values ()
            (#%plain-app (~literal fake:dynamic-recursive-contract) x:id _ ...) _ ...)
          (begin (#%plain-app (~literal fake:dynamic-recursive-contract) x:id _ ...) _ ...))
     (-x/c.tmp (syntax-e #'x))]
    [(#%plain-app (~literal fake:dynamic-recursive-contract) x:id _ ...)
     (-x/c.tmp (syntax-e #'x))]

    ;; Literals
    [(~or v:str v:number v:boolean) (-b (syntax->datum #'v))]
    ;; Ignore sub-modules
    [(module _ ...) (todo 'submodule)]
    [(module* _ ...) (todo 'module*)]
    [(#%declare _) (todo '#%declare)]
    [_
     #:when (prefab-struct-key (syntax-e #'v))
     (todo 'struct)]
    [(#%plain-app f x ...)
     (-@ (parse-e #'f)
         (parse-es #'(x ...))
         (+â„“!))]
    [((~literal with-continuation-mark) eâ‚€ eâ‚ eâ‚‚)
     (-wcm (parse-e #'eâ‚€) (parse-e #'eâ‚) (parse-e #'eâ‚‚))]
    [(begin e ...) (-begin/simp (parse-es #'(e ...)))]
    [(begin0 eâ‚€ e ...) (-begin0 (parse-e #'eâ‚€) (parse-es #'(e ...)))]
    [(if i t e) (-if (parse-e #'i) (parse-e #'t) (parse-e #'e))]
    [(let-values () b ...) (-begin/simp (parse-es #'(b ...)))]
    [(let-values (bindings ...) b ...)
     (-let-values
      (for/list ([binding (syntax->list #'(bindings ...))])
        (syntax-parse binding
          [((x ...) e) (cons (syntax->datum #'(x ...)) (parse-e #'e))]))
      (-begin/simp (parse-es #'(b ...))))]
    [(set! x e) (-set! (syntax-e #'x) (parse-e #'e))]
    [(#%plain-lambda fmls b ...+)
     (-Î» (parse-formals #'fmls) (-begin/simp (parse-es #'(b ...))))]
    
    [(case-lambda [fml bodies ...+] ...)
     (-case-Î»
      (for/list ([fmláµ¢ (syntax->list #'(fml ...))]
                 [bodiesáµ¢ (syntax->list #'((bodies ...) ...))])
        ;; Compute case arity and extended context for RHS
        (cons (parse-formals fmláµ¢) (-begin/simp (parse-es bodiesáµ¢)))))]
    [(letrec-values () b ...) (-begin/simp (parse-es #'(b ...)))]
    [(letrec-values (bindings ...) b ...)
     (-letrec-values
      (for/list ([bnd (syntax->list #'(bindings ...))])
        (syntax-parse bnd
          [((x ...) eâ‚“) (cons (syntax->datum #'(x ...)) (parse-e #'eâ‚“))]))
      (-begin/simp (parse-es #'(b ...))))]
    [(quote e) (parse-quote #'e)]
    [(quote-syntax e) (error 'parse-e "TODO: (quote-syntax ~a)" (syntax->datum #'e))]
    [((~literal #%top) . id)
     (error "Unknown identifier ~a in module ~a" (syntax->datum #'id) (cur-mod))]
    [(#%variable-reference) (todo '#%variable-reference)]
    [(#%variable-reference id) (todo (format "#%variable-reference ~a" (syntax->datum #'id)))]
    
    ;; Hacks for now. TODO: need this anymore??
    ;[(~literal null) -null]
    ;[(~literal empty) -null]
    [(~literal fake:any/c) (-ref (-ð’¾ 'any/c 'Î›) (+â„“!))]
    [(~literal fake:none/c) (-ref (-ð’¾ 'none/c 'Î›) (+â„“!))]
    [(~literal fake:not/c) (-ref (-ð’¾ 'not/c 'Î›) (+â„“!))]
    [(~literal fake:and/c) (-ref (-ð’¾ 'and/c 'Î›) (+â„“!))]
    [(~literal fake:or/c ) (-ref (-ð’¾ 'or/c  'Î›) (+â„“!))]
    
    [i:identifier
     (or
      (parse-primitive #'i)
      (match (identifier-binding #'i)
        ['lexical (-x (syntax-e #'i))]
        [#f (-x (syntax-e #'i))]
        [(list (app (Î» (x)
                      (parameterize ([current-directory (directory-part (cur-mod))])
                        ;(printf "part: ~a~n" (directory-part (cur-mod)))
                        ;(printf "id: ~a~n" #'i)
                        (mod-path->mod-name
                         (resolved-module-path-name (module-path-index-resolve x)))))
                    src)
               _ _ _ _ _ _)
         (when (equal? 'not/c (syntax-e #'i))
           (error "done"))
         (-ref (-ð’¾ (syntax-e #'i) src) (+â„“!))]))]))

(define/contract (parse-quote stx)
  (scv-syntax? . -> . -e?)
  (syntax-parse stx
    [(~or e:number e:str e:boolean e:id e:keyword) (-b (syntax-e #'e))]
    [(l . r)
     (-@ -cons
         (list (parse-quote #'l) (parse-quote #'r))
         (+â„“!))]
    [() -null]
    [e (error 'parse-quote "unsupported quoted form: ~a" (syntax-e #'e))]))

;; Parse given `formals` to extend environment
(define/contract (parse-formals formals)
  (scv-syntax? . -> . -formals?)
  (syntax-parse formals
    [(x:id ...) (syntax->datum #'(x ...))]
    [rest:id (-varargs '() (syntax-e #'rest))]
    [(x:id ... . rest:id) (-varargs (syntax->datum #'(x ...)) (syntax-e #'rest))]))

;; Return primitive with given `id`
(define/contract (parse-primitive id)
  (identifier?  . -> . (or/c #f -ref? -b?))
  (log-debug "parse-primitive: ~a~n~n" (syntax->datum id))

  (define-syntax (make-parse-clauses stx)

    (syntax-parse stx
      [(_ id:id)
       ;; The reason we generate (syntax-parse) cases instead of set lookup
       ;; is to ensure that the source refers to the right reference

       (define/contract cache (hash/c symbol? any/c) (make-hasheq))

       (define/contract (make-clause dec)
         (any/c . -> . (listof syntax?))

         (define (make-ref s)
           (symbol? . -> . syntax?)
           #`(-ref (-ð’¾ '#,s 'Î›) (+â„“!)))
         
         (match dec
           [`(#:pred ,s ,_ ...)
            (list #`[(~literal #,s) #,(hash-ref! cache s (make-ref s))])]
           [`(#:alias ,s ,t)
            (list #`[(~literal #,s)
                     #,(hash-ref! cache t (Î» () (error 'make-ref "~a aliases undeclared ~a" s t)))])]
           [`(#:batch (,ss ...) ,(? arr?) ,_ ...)
            (for/list ([s ss])
              #`[(~literal #,s) #,(hash-ref! cache s (make-ref s))])]
           [`(,(? symbol? s) ,(? base? c))
            (list #`[(~literal #,s) #,(hash-ref! cache s #`(-b #,s))])]
           [(or `(,(? symbol? s) ,_ ...)
                `(#:struct-cons ,s ,_)
                `(#:struct-pred ,s ,_)
                `(#:struct-acc  ,s ,_ ,_)
                `(#:struct-mut  ,s ,_ ,_))
            (list #`[(~literal #,s) #,(hash-ref! cache s (make-ref s))])]
           [r
            (log-warning "unhandled in `make-parse-clauses` ~a~n" r)
            '()]))
       (define ans 
         #`(syntax-parse id
             #,@(append-map make-clause prims)
             [_ #f]))
       
       ;(printf "parse-primitive:~n~a~n" (syntax->datum ans))
       
       ans]))

  ;; Read off from primitive table
  (make-parse-clauses id))

(define/contract (parse-provide-spec spec)
  (scv-syntax? . -> . -provide-spec?)
  (printf "Warning: shouldn't reach `parse-provide-spec` if using `fake-contract`~n")
  (syntax-parse spec
    [i:identifier #'i]
    [_ (error 'parse-provide-spec "unexpected: ~a" spec)]))

(define/contract (parse-require-spec spec)
  (scv-syntax? . -> . -require-spec?)
  (syntax-parse spec
    [i:identifier (syntax-e #'i)]
    [_ (log-debug "parse-require-spec: ignore ~a~n" (syntax->datum spec)) 'dummy-require]))

;; For debugging only. Return scv-relevant s-expressions
(define/contract (scv-relevant path)
  (path-string? . -> . any/c)
  (for/list ([stxáµ¢ (syntax->list (do-expand-file path))]
             #:unless (scv-ignore? stxáµ¢))
    (syntax->datum stxáµ¢)))

;; Testing only
(define (test . files)
  (files->modules files))
