#lang typed/racket/base

(provide (all-defined-out))

(require racket/match
         racket/set
         racket/function
         racket/list
         "../utils/main.rkt"
         "../utils/untyped-macros.rkt"
         "definition.rkt")

(require/typed "../primitives/declarations.rkt"
  [prims (Listof Any)])

(: fv : (U -e (Listof -e)) â†’ (â„˜ Var-Name))
;; Compute free variables for expression. Return set of variable names.
(define (fv e)
  (match e
    [(-x x) {seteq x}]
    [(-Î» xs e)
     (define bound
       (match xs
         [(-varargs zs z) (set-add (list->seteq zs) z)]
         [(? list? xs) (list->seteq xs)]))
     (-- (fv e) bound)]
    [(-@ f xs _)
     (for/fold ([FVs (fv f)]) ([x xs]) (âˆª FVs (fv x)))]
    [(-begin es) (fv es)]
    [(-begin0 eâ‚€ es) (âˆª (fv eâ‚€) (fv es))]
    [(-let-values bnds e)
     (define-values (bound FV_rhs)
       (for/fold ([bound : (â„˜ Var-Name) âˆ…eq] [FV_rhs : (â„˜ Var-Name) âˆ…eq]) ([bnd bnds])
         (match-define (cons xs rhs) bnd)
         (values (set-add-list bound xs) (âˆª FV_rhs (fv rhs)))))
     (âˆª FV_rhs (-- (fv e) bound))]
    [(-letrec-values bnds e)
     (define bound
       (for/fold ([bound : (â„˜ Var-Name) âˆ…eq]) ([bnd bnds])
         (set-add-list bound (car bnd))))
     
     (for/fold ([xs : (â„˜ Var-Name) (-- (fv e) bound)]) ([bnd bnds])
       (-- (fv (cdr bnd)) bound))]
    [(-set! x e) (set-add (fv e) x)]
    #;[(.apply f xs _) (set-union (fv f d) (fv xs d))]
    [(-if e eâ‚ eâ‚‚) (âˆª (fv e) (fv eâ‚) (fv eâ‚‚))]
    [(-amb es)
     (for/fold ([xs : (â„˜ Var-Name) âˆ…eq]) ([e es])
       (âˆª xs (fv e)))]
    [(-Î¼/c _ e) (fv e)]
    [(--> cs d _) (apply âˆª (fv d) (map fv cs))]
    [(-->i cs mk-d _) (apply âˆª (fv mk-d) (map fv cs))]
    [(-case-> clauses _)
     (for/unioneq : (â„˜ Var-Name) ([clause clauses])
       (match-define (cons cs d) clause)
       (apply âˆª (fv d) (map fv cs)))]
    [(-struct/c _ cs _)
     (for/fold ([xs : (â„˜ Var-Name) âˆ…eq]) ([c cs])
       (âˆª xs (fv c)))]
    [(? list? l)
     (for/fold ([xs : (â„˜ Var-Name) âˆ…eq]) ([e l])
       (âˆª xs (fv e)))]
    [_ (log-debug "FVâŸ¦~aâŸ§ = âˆ…~n" e) âˆ…eq]))

(module+ test
  (require typed/rackunit)
  
  (check-equal? (fv -tt) âˆ…)
  (check-equal? (fv (-Î» '(x) (-x 'x))) âˆ…)
  (check-equal? (fv (-x 'x)) {set 'x})
  (check-equal? (fv (-ð’¾ 'cons 'Î›)) âˆ…)
  (check-equal? (fv (-Î» '(x) (-Î» '(y) (-@ (-x 'f) (list (-x 'y) (-x 'x)) +â„“â‚€)))) {set 'f}))

(: closed? : -e â†’ Boolean)
;; Check whether expression is closed
(define (closed? e) (set-empty? (fv e)))

(: checks# : (Rec X (U -top-level-form
                       -e
                       -general-top-level-form
                       -e
                       -module
                       -begin/top
                       -module-level-form
                       (Listof X))) â†’ Integer)
;; Statically count number of unsafe operations needing checks
(define checks#
  (match-lambda
   [(? list? es) (for/sum : Integer ([e (in-list es)]) (checks# e))]
   [(-define-values _ e) (checks# e)]
   [(-Î» _ e) (checks# e)]
   [(-@ f xs _) (+ 1 (checks# f) (checks# xs))]
   [(-if i t e) (+ (checks# i) (checks# t) (checks# e))]
   [(-wcm k v e) (+ (checks# k) (checks# v) (checks# e))]
   [(-begin0 e es) (+ (checks# e) (checks# es))]
   [(-let-values bindings e)
    (+ (for/sum : Integer ([binding (in-list bindings)])
         (match-define (cons _ eâ‚“) binding)
         (checks# eâ‚“))
       (checks# e))]
   [(-letrec-values bindings e)
    (+ (for/sum : Integer ([binding (in-list bindings)])
         (match-define (cons _ eâ‚“) binding)
         (checks# eâ‚“))
       (checks# e))]
   [(-amb es) (for/sum ([e (in-set es)]) (checks# e))]
   [(-Î¼/c _ c) (checks# c)]
   [(--> cs d _) (+ (checks# cs) (checks# d))]
   [(-->i cs mk-d _) (+ (checks# cs) (checks# mk-d))]
   [(-case-> clauses _)
    (for/sum : Integer ([clause clauses])
      (match-define (cons cs d) clause)
      (+ (checks# cs) (checks# d)))]
   [(-struct/c _ cs _) (checks# cs)]

   [(-module _ body) (checks# body)]
   ;; FIXME count up for primitives
   [_ 0]))

(: free-x/c : -e â†’ (â„˜ Symbol))
;; Return all free references to recursive contracts inside term
(define (free-x/c e)

  (: go* : (Listof -e) â†’ (â„˜ Symbol))
  (define (go* xs) (for/unioneq : (â„˜ Symbol) ([x xs]) (go x)))

  (: go : -e â†’ (â„˜ Symbol))
  (define (go e)
    (match e
      [(-Î» xs e) (go e)]
      [(-case-Î» body)
       (for/unioneq : (â„˜ Symbol) ([p body]) (go (cdr p)))]
      [(-@ f xs ctx) (âˆª (go f) (go* xs))]
      [(-if i t e) (âˆª (go i) (go t) (go e))]
      [(-wcm k v b) (âˆª (go k) (go v) (go b))]
      [(-begin0 e es) (âˆª (go e) (go* es))]
      [(-let-values bnds e)
       (âˆª (for/unioneq : (â„˜ Symbol) ([bnd bnds]) (go (cdr bnd))) (go e))]
      [(-letrec-values bnds e)
       (âˆª (for/unioneq : (â„˜ Symbol) ([bnd bnds]) (go (cdr bnd))) (go e))]
      [(-amb es) (for/unioneq : (â„˜ Symbol) ([e es]) (go e))]
      [(-Î¼/c _ c) (go c)]
      [(--> cs d _) (âˆª (go* cs) (go d))]
      [(-->i cs mk-d _) (âˆª (go* cs) (go mk-d))]
      [(-case-> clauses _)
       (for/unioneq : (â„˜ Symbol) ([clause clauses])
         (match-define (cons cs d) clause)
         (âˆª (go d) (go* cs)))]
      [(-struct/c t cs _) (go* cs)]
      [(-x/c.tmp x) (seteq x)]
      [_ âˆ…eq]))
  
  (go e))

(: unroll : Integer -e -e â†’ -e)
;; Unroll reference to recursive contract
(define (unroll x c e)
  (let go ([e : -e e])

    (: go-bnd (âˆ€ (X) (Pairof X -e) â†’ (Pairof X -e)))
    (define (go-bnd bnd)
      (match-define (cons xs e) bnd)
      (cons xs (go e)))

    (match e
      [(-Î» xs e*) (-Î» xs (go e*))]
      [(-case-Î» clauses) (-case-Î» (map (inst go-bnd (Listof Var-Name)) clauses))]
      [(-@ f xs l) (-@ (go f) (map go xs) l)]
      [(-if eâ‚€ eâ‚ eâ‚‚) (-if (go eâ‚€) (go eâ‚) (go eâ‚‚))]
      [(-wcm k v b) (-wcm (go k) (go v) (go b))]
      [(-begin0 eâ‚€ es) (-begin0 (go eâ‚€) (map go es))]
      [(-let-values bnds e*)
       (-let-values (map (inst go-bnd (Listof Var-Name)) bnds) (go e*))]
      [(-letrec-values bnds e*)
       (-letrec-values (map (inst go-bnd (Listof Var-Name)) bnds) (go e*))]
      [(-set! z e*) (-set! z (go e*))]
      [(-amb es) (-amb (map/set go es))]
      [(-Î¼/c z e*) (if (= z x) e (-Î¼/c z (go e*)))]
      [(--> cs d â„“) (--> (map go cs) (go d) â„“)]
      [(-->i cs mk-d â„“)
       (-->i (map go cs) (assert (go mk-d) -Î»?) â„“)]
      [(-case-> clauses â„“)
       (define clauses* : (Listof (Pairof (Listof -e) -e))
         (for/list ([clause clauses])
           (match-define (cons cs d) clause)
           (cons (map go cs) (go d))))
       (-case-> clauses* â„“)]
      [(-struct/c si cs â„“) (-struct/c si (map go cs) â„“)]
      [(-x/c z) (if (= z x) c e)]
      [_
       (log-debug "unroll: ignore ~a" (show-e e))
       e])))

(: find-calls : -e (U -ð’¾ -â€¢) â†’ (â„˜ (Listof -e)))
;; Search for all invocations of `f-id` in `e`
(define (find-calls e f-id)
  (define-set calls : (Listof -e))
  (let go! : Void ([e e])
    (match e
      [(-@ f xs _)
       (go! f)
       (for-each go! xs)
       (when (equal? f f-id)
         (calls-add! xs))]
      [_ (void)]))
  calls)

(: prim-name->unsafe-prim : Symbol â†’ -o)
;; Convert primitive name to (unsafe) primitive
(define prim-name->unsafe-prim
  (let ([specials : (HashTable Symbol -o) (make-hasheq)]
        [aliases : (HashTable Symbol Symbol) (make-hasheq)]
        [mk-struct-info : (Any â†’ -struct-info)
         (match-lambda
           [`(,(? symbol? t) ,(? boolean? bs) ...)
            (-struct-info
             (-ð’¾ t 'Î›)
             (length bs)
             (for/set: : (â„˜ Natural) ([b bs] [i : Natural (in-naturals)] #:when b)
               i))])])
    (for ([dec prims])
      (match dec
        [`(#:alias ,(? symbol? x) ,(? symbol? y))
         (hash-set! aliases x y)]
        [`(#:struct-cons ,(? symbol? x) ,si)
         (hash-set! specials x (-st-mk (mk-struct-info si)))]
        [`(#:struct-pred ,(? symbol? x) ,si)
         (hash-set! specials x (-st-p (mk-struct-info si)))]
        [`(#:struct-acc ,(? symbol? x) ,si ,(? exact-nonnegative-integer? i))
         (hash-set! specials x (-st-ac (mk-struct-info si) i))]
        [`(#:struct-acc ,(? symbol? x) ,si ,(? exact-nonnegative-integer? i))
         (hash-set! specials x (-st-mut (mk-struct-info si) i))]
        [_ (void)]))
    (Î» (x)
      (cond
        [(hash-ref specials x #f)]
        [(hash-ref aliases x #f) => prim-name->unsafe-prim]
        [else x]))))

(: Î±-rename : (case->
               [-e â†’ -e]
               [-module â†’ -module]))
;; Make sure each binding has a unique name
(define (Î±-rename e)
  (define-type S->S (HashTable Symbol Symbol))
  ;; Map each bound name to its ith appearance. `0` means first, no need to rename
  (define ith : (HashTable Symbol Natural) (make-hasheq))

  (: new-binder! : S->S Var-Name â†’ (Values S->S Var-Name))
  ;; Updates the global table to remember how many times `x` has been seen,
  ;; and updates the local environment that renames free occurences of `x`
  (define (new-binder! names x)
    (cond
      [(integer? x) (values names x)]
      [else
       (cond
         [(hash-ref ith x #f) =>
          (Î» (i) (hash-set! ith x (+ 1 i)))]
         [else (hash-set! ith x 0)])
       (define x*
         (match (hash-ref ith x)
           [0 x]
           [i (format-symbol "~a~a" x (n-sub i))]))
       (values (hash-set names x x*) x*)]))

  (: new-binders! : S->S (Listof Var-Name) â†’ (Values S->S (Listof Var-Name)))
  (define (new-binders! m xs)
    (define-values (m* xs*-rev)
      (for/fold ([m : S->S m] [xs-rev : (Listof Var-Name) '()])
                ([x xs])
        (define-values (m* x*) (new-binder! m x))
        (values m* (cons x* xs-rev))))
    (values m* (reverse xs*-rev)))

  (: new-formals! : S->S -formals â†’ (values S->S -formals))
  (define (new-formals! m xs)
    (match xs
      [(-varargs zs z)
       (define-values (mâ‚ zs*) (new-binders! m zs))
       (define-values (mâ‚‚ z* ) (new-binder!  mâ‚ z))
       (values mâ‚‚ (-varargs zs* z*))]
      [(? list? xs) (new-binders! m xs)]))

  (define (go-m! [m : S->S] [modl : -module]) : -module
    (match-define (-module p forms) modl)
    (define forms*
      (for/list : (Listof -module-level-form) ([form forms])
        (match form
          [(-define-values xs e) (-define-values xs (go! m e))]
          [(-provide specs)
           (-provide
            (for/list ([spec specs])
              (match-define (-p/c-item x c â„“) spec)
              (-p/c-item x (go! m c) â„“)))]
          [(? -require? d) d]
          [(? -e? e) (go! m e)])))
    (-module p forms*))

  (define (go! [m : S->S] [e : -e]) : -e
    (match e
      [(-Î» xs e*)
       (define-values (m* xs*) (new-formals! m xs))
       (-Î» xs* (go! m* e*))]
      [(-case-Î» clauses)
       (-case-Î»
        (for/list : (Listof (Pairof (Listof Var-Name) -e)) ([clause clauses])
          (match-define (cons xs e*) clause)
          (define-values (m* xs*) (new-binders! m xs))
          (cons xs* (go! m* e*))))]
      [(-x (? symbol? x)) (-x (hash-ref m x))]
      [(-@ f xs loc) (-@ (go! m f) (map (curry go! m) xs) loc)]
      [(-if eâ‚€ eâ‚ eâ‚‚) (-if (go! m eâ‚€) (go! m eâ‚) (go! m eâ‚‚))]
      [(-wcm k v b) (-wcm (go! m k) (go! m v) (go! m b))]
      [(-begin es) (-begin (map (curry go! m) es))]
      [(-begin0 eâ‚€ es) (-begin0 (go! m eâ‚€) (map (curry go! m) es))]
      [(-let-values bnds bod)
       (define-values (m* bnds*-rev)
         (for/fold ([m* : S->S m] [bnds*-rev : (Listof (Pairof (Listof Var-Name) -e)) '()])
                   ([bnd bnds])
           (match-define (cons xs eâ‚“) bnd)
           (define-values (m** xs*) (new-binders! m* xs))
           (define eâ‚“* (go! m #|important|# eâ‚“))
           (values m** (cons (cons xs* eâ‚“*) bnds*-rev))))
       (define bod* (go! m* bod))
       (-let-values (reverse bnds*-rev) bod*)]
      [(-letrec-values bnds bod)
       (define-values (xss es) (unzip bnds))
       (define-values (m* xss*-rev)
         (for/fold ([m* : S->S m] [xss*-rev : (Listof (Listof Var-Name)) '()])
                   ([xs xss])
           (define-values (m** xs*) (new-binders! m* xs))
           (values m** (cons xs* xss*-rev))))
       (define es* (map (curry go! m*) es))
       (define bod* (go! m* bod))
       (define bnds* (map (inst cons (Listof Var-Name) -e) (reverse xss*-rev) es*))
       (-letrec-values bnds* bod*)]
      [(-set! (? symbol? x) e*) (-set! (hash-ref m x #|when `x` is top-level|# (Î» () x)) (go! m e*))]
      [(-amb es) (-amb (map/set (curry go! m) es))]
      [(-Î¼/c x c) (-Î¼/c x (go! m c))]
      [(--> cs d â„“) (--> (map (curry go! m) cs) (go! m d) â„“)]
      [(-->i cs mk-d â„“)
       (-->i (map (curry go! m) cs)
             (assert (go! m mk-d) -Î»?)
             â„“)]
      [(-case-> clauses â„“)
       (define clauses* : (Listof (Pairof (Listof -e) -e))
         (for/list ([clause clauses])
           (match-define (cons cs d) clause)
           (cons (map (curry go! m) cs) (go! m d))))
       (-case-> clauses* â„“)]
      [(-struct/c si cs â„“)
       (-struct/c si (map (curry go! m) cs) â„“)]
      [_ e]))

  (cond [(-e? e) (go! (hasheq) e)]
        [else (go-m! (hasheq) e)]))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;; Substitution
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(define-type Subst (HashTable -e -e))

(define mâˆ… : Subst (hash))

(define (e/map [m : Subst] [e : -e])

  (: go-list : Subst (Listof -e) â†’ (Listof -e))
  (define (go-list m es)
    (with-debugging/off
      ((ans) (for/list : (Listof -e) ([e es]) (go m e)))
      (printf "go-list: ~a ~a -> ~a~n" (show-subst m) (map show-e es) (map show-e ans))))

  (: go : Subst -e â†’ -e)
  (define (go m e)
    (with-debugging/off
      ((ans)
       (cond
         [(hash-empty? m) e]
         [(hash-ref m e #f) => values]
         [else
          (match e
            [(-Î» xs e*)
             (-Î» xs (go (shrink m (formals->names xs)) e*))]
            [(-case-Î» clauses)
             (define clauses*
               (for/list : (Listof (Pairof (Listof Var-Name) -e)) ([clause clauses])
                 (match-define (cons xs eâ‚“) clause)
                 (cons xs (go (shrink m (formals->names xs)) eâ‚“))))
             (-case-Î» clauses*)]
            [(-@ f xs â„“)
             (-@ (go m f) (go-list m xs) â„“)]
            [(-if eâ‚€ eâ‚ eâ‚‚)
             (-if (go m eâ‚€) (go m eâ‚) (go m eâ‚‚))]
            [(-wcm k v b)
             (-wcm (go m k) (go m v) (go m b))]
            [(-begin es)
             (-begin (go-list m es))]
            [(-begin0 eâ‚€ es)
             (-begin0 (go m eâ‚€) (go-list m es))]
            [(-let-values bnds body)
             (define-values (bnds*-rev locals)
               (for/fold ([bnds*-rev : (Listof (Pairof (Listof Var-Name) -e)) '()]
                          [locals : (â„˜ Var-Name) âˆ…eq])
                         ([bnd bnds])
                 (match-define (cons xs eâ‚“) bnd)
                 (values (cons (cons xs (go m eâ‚“)) bnds*-rev)
                         (set-add-list locals xs))))
             (define body* (go (shrink m locals) body))
             (-let-values (reverse bnds*-rev) body*)]
            [(-letrec-values bnds body)
             (define locals
               (for/fold ([locals : (â„˜ Var-Name) âˆ…eq])
                         ([bnd bnds])
                 (match-define (cons xs _) bnd)
                 (set-add-list locals xs)))
             (define m* (shrink m locals))
             (define bnds* : (Listof (Pairof (Listof Var-Name) -e))
               (for/list ([bnd bnds])
                 (match-define (cons xs eâ‚“) bnd)
                 (cons xs (go m* eâ‚“))))
             (define body* (go m* body))
             (-letrec-values bnds* body*)]
            [(-set! x e*)
             (-set! x (go m e*))]
            [(-amb es)
             (-amb (for/set: : (â„˜ -e) ([e es]) (go m e)))]
            [(-Î¼/c z c)
             (-Î¼/c z (go (shrink m {seteq z}) c))]
            [(--> cs d â„“)
             (--> (go-list m cs) (go m d) â„“)]
            [(-->i cs mk-d â„“)
             (-->i (go-list m cs) (assert (go m mk-d) -Î»?) â„“)]
            [(-case-> clauses â„“)
             (define clauses*
               (for/list : (Listof (Pairof (Listof -e) -e)) ([clause clauses])
                 (match-define (cons cs d) clause)
                 (cons (go-list m cs) (go m d))))
             (-case-> clauses* â„“)]
            [(-struct/c t cs â„“)
             (-struct/c t (go-list m cs) â„“)]
            [_
             ;(printf "unchanged: ~a @ ~a~n" (show-e e) (show-subst m))
             e])]))
      (printf "go: ~a ~a -> ~a~n" (show-subst m) (show-e e) (show-e ans))))

  (go m e))

(: e/ : (U -x -x/c.tmp) -e -e â†’ -e)
;; Substitution, where `x` can be an (open) term rather than just a free variable.
(define (e/ x eâ‚“ e) (e/map ((inst hash -e -e) x eâ‚“) e))

(: shrink : Subst (â„˜ Var-Name) â†’ Subst)
(define (shrink m xs)
  (for/fold ([m* : Subst m])
            ([eâ‚“ (in-hash-keys m)]
             #:unless (set-empty? (âˆ© xs (fv eâ‚“))))
    (hash-remove m* eâ‚“)))

(: formals->names : -formals â†’ (â„˜ Var-Name))
(define (formals->names xs)
  (cond
    [(-varargs? xs) (set-add (list->seteq (-varargs-init xs)) (-varargs-rest xs))]
    [else (list->seteq xs)]))

(define (show-subst [m : Subst]) : (Listof Sexp)
  (for/list ([(k v) m]) `(,(show-e k) â†¦ ,(show-e v))))
