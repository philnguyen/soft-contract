#lang typed/racket/base

(require racket/match
         racket/set
         racket/list
         racket/string
         racket/splicing
         racket/extflonum
         typed/racket/unit
         set-extras
         "../utils/main.rkt"
         "signatures.rkt"
         )

(provide ast-pretty-print@)
(define-unit ast-pretty-print@
  (import static-info^)
  (export ast-pretty-print^)

  (define (show-b [x : Base]) : Sexp
    (cond
      [(string? x) (format "\"~a\"" x)]
      [(or (symbol? x) (keyword? x)) `(quote ,x)]
      [(and (real? x) (inexact? x))
       (define s (number->string x))
       (substring s 0 (min (string-length s) 5))]
      [(or (regexp? x) (pregexp? x) (byte-regexp? x) (byte-pregexp? x) (bytes? x)) (format "~a" x)]
      [(extflonum? x) (extfl->inexact x)]
      [(void? x) 'void]
      [(arity-at-least? x) `(arity-at-least ,(arity-at-least-value x))]
      [(list? x) `(list ,@(map show-b x))]
      [(eof-object? x) '‚ü™eof‚ü´]
      [(path? x) (path->string x)]
      [(defined? x) x]
      [else 'undefined]))

  ;; Return operator's simple show-o for pretty-printing
  (define show-o : (-o ‚Üí Symbol)
    (match-lambda
      [(? symbol? s) s]
      [(-st-mk ùíæ) (format-symbol "_~a" (-ùíæ-name ùíæ))]
      [(-st-ac ùíæ i) (format-symbol "_~a" (struct-accessor-name ùíæ i))]
      [(-st-p ùíæ) (format-symbol "_~a?" (-ùíæ-name ùíæ))]
      [(-st-mut (== -ùíæ-mcons) 0) 'set-mcar!]
      [(-st-mut (== -ùíæ-mcons) 1) 'set-mcdr!]
      [(-st-mut (== -ùíæ-box) _) 'set-box!]
      [(-st-mut ùíæ i) (format-symbol "set-~a._~a!" (-ùíæ-name ùíæ) i)]))

  (define (show-e [e : -e]) : Sexp
    (match e
      ; syntactic sugar
      [(-Œª (-var (list x) #f) (-@ 'not (list (-@ f (list (-x x _)) _)) _) _) `(not/c ,(show-e f))]
      [(-Œª (-var (list x) #f) (-@ '= (list (-x x _) e*) _) _) `(=/c ,(show-e e*))]
      [(-Œª (-var (list x) #f) (-@ (or 'equal? 'eq? 'eqv?) (list (-x x _) e*) _) _) `(‚â°/c ,(show-e e*))]
      [(-Œª (-var (list x) #f) (-@ '> (list (-x x _) e*) _) _) `(>/c ,(show-e e*))]
      [(-Œª (-var (list x) #f) (-@ '< (list (-x x _) e*) _) _) `(</c ,(show-e e*))]
      [(-Œª (-var (list x) #f) (-@ '>= (list (-x x _) e*) _) _) `(‚â•/c ,(show-e e*))]
      [(-Œª (-var (list x) #f) (-@ '<= (list (-x x _) e*) _) _) `(‚â§/c ,(show-e e*))]
      
      [(-if a b (-b #f) _)
       (match* ((show-e a) (show-e b))
         [(`(and ,l ...) `(and ,r ...)) `(and ,@(cast l (Listof Sexp)) ,@(cast r (Listof Sexp)))]
         [(`(and ,l ...) r) `(and ,@(cast l (Listof Sexp)) ,r)]
         [(l `(and ,r ...)) `(and ,l ,@(cast r (Listof Sexp)))]
         [(l r) `(and ,l ,r)])]
      [(-if a b (-b #t) _) `(implies ,(show-e a) ,(show-e b))]

      [(-Œª xs e _) `(Œª ,(show-formals xs) ,(show-e e))]
      [(-case-Œª cases _)
       `(case-lambda
          ,@(for/list : (Listof Sexp) ([c (in-list cases)])
              (match-define (-Œª (-var xs ?z) e _) c)
              (if ?z
                  `[,(cons xs ?z) ,(show-e e)]
                  `[,xs ,(show-e e)])))]
      [(-‚Ä¢) '‚Ä¢]
      [(-b b) (show-b b)]
      [(? -o? o) (show-o o)]
      [(-x x _) (if (symbol? x) x (-ùíæ-name x))]
      [(-let-values bnds body _)
       (match bnds
         [(list (cons (list lhs) rhs) ...)
          `(let ,(for/list : (Listof Sexp) ([x (in-list lhs)]
                                            [e (in-list rhs)])
                   `(,(assert x symbol?) ,(show-e (assert e -e?))))
             ,(show-e body))]
         [_
          `(let-values
               ,(for/list : (Listof Sexp) ([bnd bnds])
                  (match-define (cons xs ex) bnd)
                  `(,xs ,(show-e ex)))
             ,(show-e body))])]
      [(-letrec-values bnds body _)
       (match bnds
         [(list (cons (list lhs) rhs) ...)
          `(letrec ,(for/list : (Listof Sexp) ([x (in-list lhs)]
                                               [e (in-list rhs)])
                      `(,(assert x symbol?) ,(show-e (assert e -e?))))
             ,(show-e body))]
         [_
          `(letrec-values
               ,(for/list : (Listof Sexp) ([bnd bnds])
                  (match-define (cons xs ex) bnd)
                  `(,xs ,(show-e ex)))
             ,(show-e body))])]
      [(-set! x e _) `(set! ,(if (symbol? x) x (-ùíæ-name x)) ,(show-e e))]
      [(-@ f xs _) `(,(show-e f) ,@(show-es xs))]
      [(-begin es) `(begin ,@(show-es es))]
      [(-begin0 e es) `(begin0 ,(show-e e) ,@(show-es es))]
      [(-error msg _) `(error ,msg)]
      #;[(-apply f xs _) `(apply ,(show-e f) ,(go show-e xs))]
      [(-if i t e _) `(if ,(show-e i) ,(show-e t) ,(show-e e))]
      [(-Œº/c x c) `(Œº/c (,x) ,(show-e c))]
      [(-->i (-var cs c) d)
       `(->i ,@(map show-dom cs)
             ,@(if c `(#:rest ,(show-dom c)) '())
             ,(match d
                [#f 'any]
                [(list d) (show-dom d)]
                [(? values ds) `(values ,@(map show-dom ds))]))]
      [(case--> cases) `(case-> ,@(map show-e cases))]
      [(-x/c.tmp x) x]
      [(-‚àÄ/c xs c _) `(parametric->/c ,xs ,(show-e c))]))

  (: show-dom : -dom ‚Üí Sexp)
  (define show-dom
    (match-lambda
      [(-dom x ?xs d _)
       (if ?xs `(,x ,?xs ,(show-e d)) `(,x ,(show-e d)))]))

  (define (show-es [es : (Sequenceof -e)]) : (Listof Sexp)
    (for/list ([e es]) (show-e e)))

  (define (show-module [m : -module]) : (Listof Sexp)
    (match-define (-module path forms) m)
    `(module ,path scv:lang
       ,@(map show-module-level-form forms)))

  (define show-module-level-form : (-module-level-form ‚Üí Sexp)
    (match-lambda
      [(-provide specs) `(provide ,@(map show-provide-spec specs))]
      [(? -general-top-level-form? m) (show-general-top-level-form m)]
      [(? -module? m) (show-module m)]))

  (define show-general-top-level-form : (-general-top-level-form ‚Üí Sexp)
    (match-lambda
      [(? -e? e) (show-e e)]
      [(-define-values xs e _)
       (match* (xs e)
         [((list f) (-Œª xs e* _)) `(define (,f ,@(show-formals xs)) ,(show-e e*))]
         [((list x) _) `(define ,x ,(show-e e))]
         [(_ _) `(define-values ,xs ,(show-e e))])]
      [(-require specs) `(require ,@(map show-require-spec specs))]))

  (define show-provide-spec : (-provide-spec ‚Üí Sexp)
    (match-lambda
      [(-p/c-item x c _) `(,x ,(show-e c))]
      [(? symbol? x) x]))

  (define show-require-spec : (-require-spec ‚Üí Sexp)
    values)

  (define show-formals : (-formals ‚Üí Sexp)
    (match-lambda
      [(-var xs (? values x)) (cons xs x)]
      [(-var xs _) xs]))

  (define show-ùíæ : (-ùíæ ‚Üí Symbol)
    (match-lambda
      [(-ùíæ name from) (format-symbol "~a@~a" name from)]))

  (: show-values-lift (‚àÄ (X) (X ‚Üí Sexp) ‚Üí (Listof X) ‚Üí Sexp))
  (define (show-values-lift show-elem)
    (match-lambda
      [(list x) (show-elem x)]
      [xs `(values ,@(map show-elem xs))]))

  (define show-values (show-values-lift show-e))

  (define (show-subst [m : Subst]) : (Listof Sexp)
    (for/list ([(k v) m]) `(,k ‚Ü¶ ,(show-e v))))
  )
