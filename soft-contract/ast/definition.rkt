#lang typed/racket/base

(provide (all-defined-out)
         (all-from-out "arity.rkt"))

(require racket/match
         racket/set
         racket/list
         racket/string
         racket/extflonum 
         racket/splicing
         "../utils/main.rkt"
         "arity.rkt")

(require/typed/provide racket/undefined
  [undefined Undefined])

;; Parameterized begin
(struct (X) -begin ([body : (Listof X)]) #:transparent)
(define-type -begin/e (-begin -e))
(define-type -begin/top (-begin -top-level-form))

;; Temporary definition of module path
(define-type -l (U Symbol String))
(struct -l¬≥ ([pos : -l] [neg : -l] [src : -l]) #:transparent)

;; Swap positive and negative blame parties
(define swap-parties : (-l¬≥ ‚Üí -l¬≥)
  (match-lambda [(-l¬≥ l+ l- lo) (-l¬≥ l- l+ lo)]))

(define-new-subtype -‚Ñì (+‚Ñì Natural))

(splicing-local
    ((define n : Natural 1)
     (define m : (HashTable (Listof Any) -‚Ñì) (make-hash))
     ;; Just for debugging
     (define m‚Åª¬π : (HashTable -‚Ñì (Listof Any)) (make-hasheq)))

  (: +‚Ñì! : ‚Üí -‚Ñì)
  (define (+‚Ñì!)
    (begin0 (+‚Ñì n)
      (set! n (+ 1 n))))

  ;; Hack to remember fixed location for havoc
  (: +‚Ñì/memo! : (U 'hv-res 'hv-ref 'hv-ap 'opq-ap 'ac-ap 'vref) Any * ‚Üí -‚Ñì)
  (define (+‚Ñì/memo! tag . xs)
    (define ‚Ñì (hash-ref! m (cons tag xs) +‚Ñì!))
    (hash-set! m‚Åª¬π ‚Ñì (cons tag xs))
    ‚Ñì)

  (: +‚Ñì/ctc : -‚Ñì Natural ‚Üí -‚Ñì)
  (define (+‚Ñì/ctc ‚Ñì i)
    (define ‚Ñì‚Çê (hash-ref! m (list ‚Ñì i) +‚Ñì!))
    (hash-set! m‚Åª¬π ‚Ñì‚Çê (list ‚Ñì i))
    ‚Ñì‚Çê)

  (: ‚Ñì‚Åª¬π : -‚Ñì ‚Üí Any)
  ;; Just for debugging
  (define (‚Ñì‚Åª¬π ‚Ñì)
    (hash-ref m‚Åª¬π ‚Ñì (Œª () (error '‚Ñì‚Åª¬π "nothing for ~a" ‚Ñì))))
)

(define +‚Ñì‚ÇÄ (+‚Ñì 0))

(: +x! : (U Symbol Integer) * ‚Üí Symbol)
(define (+x! . prefixes)
  (define (stuff->string x) (format "~a" x))
  (define prefix (string-join (map stuff->string prefixes) "_" #:after-last "_"))
  (gensym prefix))

(: +x!/memo : (U Symbol Integer) * ‚Üí Symbol)
(define +x!/memo
  (let ([m : (HashTable (Listof (U Symbol Integer)) Symbol) (make-hash)])
    (Œª [xs : (U Symbol Integer) *]
      (hash-ref! m xs (Œª () (apply +x! xs))))))

;; Identifier as a name and its source
(struct -ùíæ ([name : Symbol] [ctx : -l]) #:transparent)

;; Formal parameters
(-formals . ::= . (Listof Symbol)
                  (-varargs [init : (Listof Symbol)] [rest : Symbol]))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;; AST subset definition as in Racket reference 1.2.3.1
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(Base . ::= . Number ExtFlonum Boolean String Symbol Keyword Bytes Regexp PRegexp Char Null Void Arity EOF Undefined)

(-top-level-form . ::= . -general-top-level-form
                         -e
                         -module
                         -begin/top)

(-module-level-form . ::= . -general-top-level-form
                            (-provide [specs : (Listof -provide-spec)])
                            -submodule-form)

(-general-top-level-form . ::= . -e
                                 (-define-values [ids : (Listof Symbol)] [e : -e])
                                 (-require (Listof -require-spec)))

(-submodule-form . ::= . (-module [path : -l] [body : (Listof -module-level-form)]))

(-provide-spec . ::= . (-p/c-item [id : Symbol] [spec : -e] [loc : -‚Ñì]))

(-require-spec . ::= . -l #|TODO|#)

(-e . ::= . -v
            (-x Symbol) ; lexical variables 
            -ùíæ ; module references
            (-@ -e (Listof -e) -‚Ñì)
            (-if -e -e -e)
            (-wcm [key : -e] [val : -e] [body : -e])
            -begin/e
            (-begin0 -e (Listof -e))
            (-quote Any)
            (-let-values [bnds : (Listof (Pairof (Listof Symbol) -e))] [body : -e])
            (-letrec-values [bnds : (Listof (Pairof (Listof Symbol) -e))] [body : -e])
            (-set! (U -ùíæ -x) -e)
            (-error String)
            (-amb (‚Ñò -e))
            
            ;; contract stuff
            (-Œº/c Symbol -e)
            (--> [doms : (Listof -e)] [rng : -e] [pos : -‚Ñì])
            (-->i [doms : (Listof -e)] [rng : -Œª] [pos : -‚Ñì])
            (-case-> [clauses : (Listof (Pairof (Listof -e) -e))] -‚Ñì)
            (-x/c.tmp Symbol) ; hack
            (-x/c Symbol)
            (-struct/c [name : -ùíæ] [fields : (Listof -e)] [pos : -‚Ñì])

            ;; internal use only
            (-ar -e -e))

(-v . ::= . -prim
            (-Œª -formals -e)
            (-case-Œª (Listof (Pairof (Listof Symbol) -e)))
            (-‚Ä¢ Natural))

(-prim . ::= . ;; Represent *unsafe* operations without contract checks. 
               ;; User code shouldn't have direct access to these.
               ;; Generated `prim` module exports these operations wrapped in contract. -o (-b Base)
               -o
               ;; primitive values that can appear in syntax
               (-b [unboxed : Base]))

(-o . ::= . Symbol
           (-st-p -ùíæ)
           (-st-ac -ùíæ Index)
           (-st-mut -ùíæ Index)
           (-st-mk -ùíæ)
           ;; internal use only
           (-st/c-ac -ùíæ Index)
           (-->i-ac-dom Index)
           (-->i-ac-rng)
           (-->-ac-dom Index)
           (-->-ac-rng)
           (-ar-ctc)
           (-ar-fun))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;; Constants & 'Macros'
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(define -tt (-b #t))
(define -ff (-b #f))
(define -null (-b null))
(define -void (-b (void)))
(define -undefined (-b undefined))

(define -ùíæ-values (-ùíæ 'values 'Œõ))
(define -ùíæ-cons (-ùíæ 'cons 'Œõ))
(define -cons (-st-mk -ùíæ-cons))
(define -car (-st-ac -ùíæ-cons 0))
(define -cdr (-st-ac -ùíæ-cons 1))
(define -cons? (-st-p -ùíæ-cons))

(define -zero (-b 0))
(define -one (-b 1))

(define -ùíæ-box (-ùíæ 'box 'Œõ))
(define -box? (-st-p -ùíæ-box))
(define -unbox (-st-ac -ùíæ-box 0))
(define -box (-st-mk -ùíæ-box))
(define -set-box! (-st-mut -ùíæ-box 0))
(define -pred (--> (list 'any/c) 'boolean? +‚Ñì‚ÇÄ))

(define havoc-path 'havoc)
(define havoc-ùíæ (-ùíæ 'havoc-id havoc-path))

(: -cond : (Listof (Pairof -e -e)) -e ‚Üí -e)
;; Make `cond` at object language level, expanding to `if`
(define (-cond cases default)
  (foldr (Œª ([alt : (Pairof -e -e)] [els : -e])
           (match-define (cons cnd thn) alt)
           (-if cnd thn els))
         default
         cases))

(: -->* : (Listof -e) -e -e ‚Üí -e)
;; Make a non-dependent vararg contract
;; TODO: separate case for non-dependent varargs
(define (-->* cs rst d)
  (define xs (-varargs (map (Œª (_) (+x!)) cs) (+x!)))
  (-->i (append cs (list rst)) (-Œª xs d) (+‚Ñì!)))

;; Make conjunctive and disjunctive contracts
(splicing-local
    ((: -app/c : Symbol ‚Üí (Listof -e) ‚Üí -e)
     (define ((-app/c o) es)
       (let go ([es : (Listof -e) es])
         (match es
           ['() 'any/c]
           [(list e) e]
           [(cons e es*) (-@ o (list e (go es*)) (+‚Ñì!))]))))
  (define -and/c (-app/c 'and/c))
  (define -or/c (-app/c 'or/c)))

(: -one-of/c : (Listof -e) ‚Üí -e)
(define (-one-of/c es)
  (cond
    [(null? es) 'none/c]
    [else
     (define x (+x!))
     (define ùê± (-x x))
     (define body : -e
       (let build-body ([es : (Listof -e) es])
         (match es
           [(list e) (-@ 'equal? (list ùê± e) (+‚Ñì!))]
           [(cons e es*)
            (-if (-@ 'equal? (list ùê± e) (+‚Ñì!))
                 -tt
                 (build-body es*))])))
     (-Œª (list x) body)]))

(: -cons/c : -e -e ‚Üí -e)
(define (-cons/c c d)
  (-struct/c -ùíæ-cons (list c d) (+‚Ñì!)))

(: -listof : -e ‚Üí -e)
(define (-listof c)
  (define x (+x! 'rec))
  (-Œº/c x (-or/c (list 'null? (-cons/c c (-x/c x))))))

(: -box/c : -e ‚Üí -e)
(define (-box/c c)
  (-struct/c -ùíæ-box (list c) (+‚Ñì!)))

(: -list/c : (Listof -e) ‚Üí -e)
(define (-list/c cs)
  (foldr -cons/c 'null? cs))

(: -list : (Listof -e) ‚Üí -e)
(define (-list es)
  (match es
    ['() -null]
    [(cons e es*)
     (-@ -cons (list e (-list es*)) (+‚Ñì!))]))

(:* -and : -e * ‚Üí -e)
;; Return ast representing conjuction of 2 expressions
(define -and
  (match-lambda*
    [(list) -tt]
    [(list e) e]
    [(cons e es) (-if e (apply -and es) -ff)]))

(: -comp/c : Symbol -e ‚Üí -e)
;; Return ast representing `(op _ e)`
(define (-comp/c op e)
  (define x (+x!))
  (define ùê± (-x x))
  (-Œª (list x)
      (-and (-@ 'real? (list ùê±) (+‚Ñì!)) (-@ op (list ùê± e) (+‚Ñì!)))))

(: -amb/simp : (Listof -e) ‚Üí -e)
;; Smart constructor for `amb` with simplification for 1-expression case
(define -amb/simp
  (match-lambda
    [(list e) e]
    [es (-amb (list->set es))]))

(: -amb/remember : (Listof -e) ‚Üí -e)
;; Return ast representing "remembered" non-determinism
(define/match (-amb/remember es)
  [((list)) (-b 'end-of-amb)]
  [((list e)) e]
  [((cons e es)) (-if (‚Ä¢!) e (-amb/remember es))])

(: -begin/simp : (‚àÄ (X) (Listof X) ‚Üí (U X (-begin X))))
;; Smart constructor for begin, simplifying single-expression case
(define/match (-begin/simp xs)
  [((list e)) e]
  [(es) (-begin es)])

(: ‚Ä¢! : ‚Üí -‚Ä¢)
;; Generate new labeled hole
(define ‚Ä¢!
  (let ([n : Natural 0])
    (Œª () (begin0 (-‚Ä¢ n) (set! n (+ 1 n))))))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;; Pretty Printing
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(define (show-‚Ñì [‚Ñì : -‚Ñì]) : Symbol
  (format-symbol "‚Ñì~a" (n-sub ‚Ñì)))

(define (show-b [x : Base]) : Sexp
  (cond
    [(string? x) (format "\"~a\"" x)]
    [(or (symbol? x) (keyword? x)) `(quote ,x)]
    [(and (real? x) (inexact? x))
     (define s (number->string x))
     (substring s 0 (min (string-length s) 5))]
    [(or (regexp? x) (pregexp? x) (bytes? x)) (format "~a" x)]
    [(extflonum? x) (extfl->inexact x)]
    [(void? x) 'void]
    [(arity-at-least? x) `(arity-at-least ,(arity-at-least-value x))]
    [(list? x) `(list ,@(map show-b x))]
    [(eof-object? x) '‚ü™eof‚ü´]
    [(defined? x) x]
    [else 'undefined]))

;; Return operator's simple show-o for pretty-printing
(define show-o : (-o ‚Üí Symbol)
  (match-lambda
   [(? symbol? s) s]
   [(-st-mk ùíæ) (-ùíæ-name ùíæ)]
   [(== -car) 'car]
   [(== -cdr) 'cdr]
   [(== -unbox) 'unbox]
   [(-st-ac ùíæ i) (format-symbol "~a@~a" (-ùíæ-name ùíæ) i)]
   [(-st-p ùíæ) (format-symbol "~a?" (-ùíæ-name ùíæ))]
   [(== -set-box!) 'set-box!]
   [(-st-mut ùíæ i) (format-symbol "set-~a-~a!" (-ùíæ-name ùíæ) i)]
   ;; internals
   [(-st/c-ac ùíæ i) (format-symbol "~a/c@~a" (-ùíæ-name ùíæ) i)]
   [(-->i-ac-dom i) (format-symbol "->i~a" (n-sub i))]
   [(-->i-ac-rng) '->i·µ£]
   [(-->-ac-dom i) (format-symbol "->~a" (n-sub i))]
   [(-->-ac-rng) '->·µ£]
   [(-ar-ctc) 'ar-ctc]
   [(-ar-fun) 'ar-fun]))

(define (show-e [e : -e]) : Sexp
  (match e
    ; syntactic sugar
    [(-Œª (list x) (-@ 'not (list (-@ f (list (-x x)) _)) _)) `(not/c ,(show-e f))]
    [(-Œª (list x) (-@ '= (list (-x x) e*) _)) `(=/c ,(show-e e*))]
    [(-Œª (list x) (-@ (or 'equal? 'eq? 'eqv?) (list (-x x) e*) _)) `(‚â°/c ,(show-e e*))]
    [(-Œª (list x) (-@ '> (list (-x x) e*) _)) `(>/c ,(show-e e*))]
    [(-Œª (list x) (-@ '< (list (-x x) e*) _)) `(</c ,(show-e e*))]
    [(-Œª (list x) (-@ '>= (list (-x x) e*) _)) `(‚â•/c ,(show-e e*))]
    [(-Œª (list x) (-@ '<= (list (-x x) e*) _)) `(‚â§/c ,(show-e e*))]
       
    [(-if a b (-b #f))
     (match* ((show-e a) (show-e b))
       [(`(and ,l ...) `(and ,r ...)) `(and ,@(cast l Sexps) ,@(cast r Sexps))]
       [(`(and ,l ...) r) `(and ,@(cast l Sexps) ,r)]
       [(l `(and ,r ...)) `(and ,l ,@(cast r Sexps))]
       [(l r) `(and ,l ,r)])]
    [(-if a b (-b #t)) `(implies ,(show-e a) ,(show-e b))]

    [(-Œª xs e) `(Œª ,(show-formals xs) ,(show-e e))]
    [(-case-Œª clauses)
     `(case-lambda
        ,@(for/list : (Listof Sexp) ([clause clauses])
            (match-define (cons xs e) clause)
            `(,(show-formals xs) ,(show-e e))))]
    [(-‚Ä¢ i) (format-symbol "‚Ä¢~a" (n-sub i))]
    [(-b b) (show-b b)]
    [(? -o? o) (show-o o)]
    [(-x x) x]
    [(-ùíæ x p)
     (case p ;; hack
       [(Œõ) (format-symbol "_~a" x)]
       [else x])]
    [(-let-values bnds body)
     `(let-values
          ,(for/list : (Listof Sexp) ([bnd bnds])
             (match-define (cons xs ex) bnd)
             `(,xs ,(show-e ex)))
        ,(show-e body))]
    [(-letrec-values bnds body)
     `(letrec-values
          ,(for/list : (Listof Sexp) ([bnd bnds])
             (match-define (cons xs ex) bnd)
             `(,xs ,(show-e ex)))
        ,(show-e body))]
    [(-set! x e) `(set! ,(show-e x) ,(show-e e))]
    [(-@ f xs _) `(,(show-e f) ,@(show-es xs))]
    [(-begin es) `(begin ,@(show-es es))]
    [(-begin0 e es) `(begin ,(show-e e) ,@(show-es es))]
    [(-error msg) `(error ,msg)]
    #;[(-apply f xs _) `(apply ,(show-e f) ,(go show-e xs))]
    [(-if i t e) `(if ,(show-e i) ,(show-e t) ,(show-e e))]
    [(-amb e*) `(amb ,@(for/list : (Listof Sexp) ([e e*]) (show-e e)))]
    [(-Œº/c x c) `(Œº/c (,x) ,(show-e c))]
    [(--> cs d _)
     `(,@(map show-e cs) . -> . ,(show-e d))]
    [(-->i cs (and d (-Œª xs _)) _)
     (match xs
       [(? list? xs)
        `(,@(map show-e cs) ‚Ü¶ ,(show-e d))]
       [(-varargs xs‚ÇÄ x)
        (define-values (cs‚ÇÄ c) (split-at cs (length xs‚ÇÄ)))
        `(,@(map show-e cs‚ÇÄ) #:rest ,@(map show-e c) ‚Ü¶ ,(show-e d))])]
    [(-case-> clauses _)
     (for/list : (Listof Sexp) ([clause clauses])
       (match-define (cons cs d) clause)
       `(,@(map show-e cs) . -> . ,(show-e d)))]
    [(-x/c.tmp x) x]
    [(-x/c x) x]
    [(-struct/c ùíæ cs _)
     `(,(format-symbol "~a/c" (-ùíæ-name ùíæ)) ,@(show-es cs))]
    ;; internals
    [(-ar c e) `(ar ,(show-e c) ,(show-e e))]))

(define (show-es [es : (Sequenceof -e)]) : (Listof Sexp)
  (for/list ([e es]) (show-e e)))

(define (show-module [m : -module]) : (Listof Sexp)
  (match-define (-module path forms) m)
  `(module ,path
    ,@(map show-module-level-form forms)))

(define show-module-level-form : (-module-level-form ‚Üí Sexp)
  (match-lambda
    [(-provide specs) `(provide ,@(map show-provide-spec specs))]
    [(? -general-top-level-form? m) (show-general-top-level-form m)]))

(define show-general-top-level-form : (-general-top-level-form ‚Üí Sexp)
  (match-lambda
    [(? -e? e) (show-e e)]
    [(-define-values xs e)
     (match* (xs e)
       [((list f) (-Œª xs e*)) `(define (,f ,@(show-formals xs)) ,(show-e e*))]
       [((list x) _) `(define ,x ,(show-e e))]
       [(_ _) `(define-values ,xs ,(show-e e))])]
    [(-require specs) `(require ,@(map show-require-spec specs))]))

(define show-provide-spec : (-provide-spec ‚Üí Sexp)
  (match-lambda
    [(-p/c-item x c _) `(,x ,(show-e c))]))

(define show-require-spec : (-require-spec ‚Üí Sexp)
  values)

(define show-formals : (-formals ‚Üí Sexp)
  (match-lambda
    [(-varargs xs rst) (cons xs rst)]
    [(? list? l) l]))

(define (show-e-map [m : (HashTable -e -e)]) : (Listof Sexp)
  (for/list ([(x y) m]) `(,(show-e x) ‚Ü¶ ,(show-e y))))
