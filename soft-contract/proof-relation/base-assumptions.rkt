#lang typed/racket/base

;; This module defines facilities for asserting and querying assumptions
;; about primitive operations, namely:
;; - implications and exclusions between primitive predicates
;; - (conservative) ranges of primitive operations
;; - arities of primitive operations

(require racket/match
         racket/set
         set-extras
         "../utils/def.rkt"
         "../utils/map.rkt"
         "../utils/debug.rkt"
         "../ast/arity.rkt"
         "../runtime/verification-result.rkt")

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;; Implication and Exclusion
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(provide add-implication! add-exclusion!
         get-weakers get-strongers get-exclusions oâ‡’o)

(define implication-table : (HashTable Symbol (â„˜ Symbol)) (make-hasheq))
(define exclusion-table : (HashTable Symbol (â„˜ Symbol)) (make-hasheq))
(define implication-tableâ»Â¹ : (HashTable Symbol (â„˜ Symbol)) (make-hasheq))

(: add-implication! : Symbol Symbol â†’ Void)
;; Extend implication table and take care of transitivity
(define (add-implication! p q)
  (unless (map-has? implication-table p q)
    (map-add! implication-table   p q #:eq? #t)
    (map-add! implication-tableâ»Â¹ q p #:eq? #t)
    ;; implication is reflexive
    (add-implication! p p)
    (add-implication! q q)
    ;; implication is transitive
    (for ([q* (in-set (get-weakers q))])
      (add-implication! p q*))
    (for ([pâ‚€ (in-set (get-strongers p))])
      (add-implication! pâ‚€ q))
    ;; (r â†’ Â¬q) and (qâ‚€ â†’ q) implies r â†’ Â¬qâ‚€
    (for* ([r (in-set (get-exclusions q))])
      (add-exclusion! p r))))

(: add-exclusion! : Symbol Symbol â†’ Void)
;; Extend exclusion table and take care of inferring existing implication
(define (add-exclusion! p q)
  (unless (map-has? exclusion-table p q)
    (map-add! exclusion-table p q #:eq? #t)
    ;; (p â†’ Â¬q) and (qâ‚€ â†’ q) implies (p â†’ Â¬qâ‚€)
    (for ([qâ‚€ (in-set (get-strongers q))])
      (add-exclusion! p qâ‚€))
    (for ([pâ‚€ (in-set (get-strongers p))])
      (add-exclusion! pâ‚€ q))
    ;; exclusion is symmetric
    (add-exclusion! q p)))

(:* get-weakers get-strongers get-exclusions : Symbol â†’ (â„˜ Symbol))
(define (get-weakers    p) (hash-ref implication-table   p mk-âˆ…eq))
(define (get-strongers  p) (hash-ref implication-tableâ»Â¹ p mk-âˆ…eq))
(define (get-exclusions p) (hash-ref exclusion-table     p mk-âˆ…eq))

(: oâ‡’o : Symbol Symbol â†’ -R)
(define (oâ‡’o p q)
  (cond [(eq? p q) 'âœ“]
        [(âˆ‹ (get-weakers p) q) 'âœ“]
        [(âˆ‹ (get-exclusions p) q) 'âœ—]
        [else '?]))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;; Range
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(provide set-range! get-conservative-range
         set-partial! partial-prims)

(define range-table : (HashTable Symbol Symbol) (make-hasheq))
(define partial-prims : (HashTable Symbol Natural) (make-hasheq))

(: set-range! : Symbol Symbol â†’ Void)
(define (set-range! o r) (hash-set-once! range-table o r))

(: set-partial! : Symbol Natural â†’ Void)
(define (set-partial! o n) (hash-set! partial-prims o n))

(: get-conservative-range : Symbol â†’ Symbol)
(define (get-conservative-range o) (hash-ref range-table o (Î» () 'any/c)))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;; Arity
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(provide update-arity! get-arity V-arity formals-arity guard-arity)

(define arity-table : (HashTable Symbol Arity) (make-hasheq))

(: update-arity! : Symbol Arity â†’ Void)
(define (update-arity! o a)
  (cond [(hash-ref arity-table o #f) =>
         (Î» ([aâ‚€ : Arity])
           (unless (arity-includes? aâ‚€ a)
             (hash-set! arity-table o (normalize-arity (list aâ‚€ a)))))]
        [else
         (hash-set! arity-table o a)]))

(: get-arity : Symbol â†’ Arity)
(define (get-arity o) (hash-ref arity-table o (Î» () (error 'get-arity "nothing for ~a" o))))

;;;;; TODO stuff below migrated from `runtime` to here
;;;;; not the most appropriate place...

(require (only-in racket/list remove-duplicates)
         "../ast/definition.rkt"
         "../ast/static-info.rkt"
         "../runtime/definition.rkt")

(define formals-arity : (-formals â†’ Arity)
  (match-lambda
    [(-var init _) (arity-at-least (length init))]
    [(? list? xs) (length xs)]))

(define guard-arity : (-=>_ â†’ Arity)
  (match-lambda
    [(-=> Î±s _ _) (shape Î±s)]
    [(and grd (-=>i Î±s (list mk-D mk-d _) _))
     (match mk-D
       [(-Clo xs _ _ _) (formals-arity xs)]
       [_
        ;; FIXME: may be wrong for var-args. Need to have saved more
        (length Î±s)])]))

(: V-arity : -V â†’ (Option Arity))
;; Return given value's arity, or `#f` if it's not a procedure
(define V-arity
  (match-lambda
    [(-Clo xs _ _ _) (formals-arity xs)]
    [(-Case-Clo clauses _ _)
     (remove-duplicates
      (for/list : (Listof Natural) ([clause clauses])
        (match-define (cons xs _) clause)
        (length xs)))]
    [(or (-And/C #t _ _) (-Or/C #t _ _) (? -Not/C?) (-St/C #t _ _) (? -One-Of/C?)) 1]
    [(-Ar guard _ _) (guard-arity guard)]
    [(? -st-p?) 1]
    [(-st-mk ğ’¾) (get-struct-arity ğ’¾)]
    [(? -st-ac?) 1]
    [(? -st-mut?) 2]
    [(? symbol? o) (hash-ref arity-table o)]
    [(-â— _) #f]
    [V
     (printf "Warning: call `V-arity` on an obviously non-procedure ~a" (show-V V))
     #f]))
